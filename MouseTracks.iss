; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; Command: ISCC.exe /DMyAppVersion="2.0.0" /DMySourceBaseName="dist\MouseTracks-2.0.0-windows-x64" /DMyDestinationBaseName="dist\MouseTracksSetup" MouseTracks.iss

#define MyAppName "MouseTracks"

; You can either hardcode the version here or pass it in via command line
; using /DMyAppVersion="2.0.0" when compiling.
#ifndef MyAppVersion
  #define MyAppVersion "2.0.0"
#endif

; Define the full path to the source executable (without extension)
; Example: dist\MouseTracks-2.0.0-windows-x64
#ifndef MySourceBaseName
  #define MySourceBaseName "dist\MouseTracks"
#endif

; Define the full path to the output installer (without extension)
#ifndef MyDestinationBaseName
  #define MyDestinationBaseName "dist\MouseTracksSetup"
#endif

#define MyAppPublisher "Peter Hunt"
#define MyAppURL "https://github.com/huntfx/MouseTracks"

[Setup]
AppId={{4FABD8FD-8B74-42D0-A4B6-A8F96BDCBBAD}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}/issues
AppUpdatesURL={#MyAppURL}/releases

UninstallDisplayName={#MyAppName}
UninstallDisplayIcon={app}\icon.ico

DefaultDirName={localappdata}\{#MyAppName}
; Support appdata installs only
PrivilegesRequired=lowest
; Create a "MouseTracks" folder in the Start Menu
DefaultGroupName={#MyAppName}

DisableDirPage=yes
DisableProgramGroupPage=yes
DisableWelcomePage=no

Compression=lzma2
SolidCompression=yes

; Dynamically extract the directory and filename from MyDestinationBaseName
OutputDir={#ExtractFilePath(MyDestinationBaseName)}
OutputBaseFilename={#ExtractFileName(MyDestinationBaseName)}

SetupIconFile=resources\images\icon.ico
LicenseFile=LICENSE.md

ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible

; Tells the installer to check if the app is running
CloseApplications=yes
RestartApplications=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "autostart"; Description: "Launch {#MyAppName} automatically when Windows starts"; GroupDescription: "Startup options:"
Name: "startminimized"; Description: "Start minimised to the system tray"; GroupDescription: "Startup options:"

[Registry]
; Write to the standard Windows "Run" key for the current user
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "{#MyAppName}"; ValueData: """{app}\{#MyAppName}.exe"" --autostart{code:GetStartupArgs}"; Flags: uninsdeletevalue; Tasks: autostart

[Files]
; --- File 1: The Launcher ---
; This is the fixed entry point (MouseTracks.exe).
Source: "dist\MouseTracks.exe"; DestDir: "{app}"; DestName: "{#MyAppName}.exe"; Flags: ignoreversion

; --- File 2: The Versioned Executable ---
Source: "{#}.exe"; DestDir: "{app}"; Flags: ignoreversion

Source: "README.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "LICENSE.md"; DestDir: "{app}"; Flags: ignoreversion

; Source: "config\*"; DestDir: "{app}\config"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "resources\images\icon.ico"; DestDir: "{app}"; DestName: "icon.ico"; Flags: ignoreversion

[Icons]
; Create the Start Menu shortcut
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppName}.exe"
; Create the Desktop shortcut (optional, based on Tasks)
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppName}.exe"; Tasks: desktopicon

[Run]
; Launch the app after installation is complete
Filename: "{app}\{#MyAppName}.exe"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
// Function to dynamically determine startup arguments based on selected tasks
function GetStartupArgs(Param: String): String;
begin
  Result := '';
  // Check if the "Start minimized" task is checked
  if WizardIsTaskSelected('startminimized') then
  begin
    // Append the flag with a leading space
    Result := ' --start-hidden';
  end;
end;
